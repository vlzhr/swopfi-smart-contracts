{-# STDLIB_VERSION 4 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}

let governanceAddress = Address(base58'3PLHVWCqA9DJPDbadUofTohnCULLauiDWhS')
let farmingAddress = Address(base58'3P73HDkPqG15nLXevjCbmXtazHYTZbpPoPw')
let oracleAddress = Address(base58'3PEbqViERCoKnmcSULh6n2aiMvUdSQdCsom')
let adminInvoke = base58'Czn4yoAuUZCVCLJDRfskn8URfkwpknwBTZDbs1wFrY7h'
let adminPubKey1 = base58'DXDY2itiEcYBtGkVLnkpHtDFyWQUkoLJz79uJ7ECbMrA'
let adminPubKey2 = base58'E6Wa1SGoktYcjHjsKrvjMiqJY3SWmGKcD8Q5L8kxSPS7'
let adminPubKey3 = base58'AZmWJtuy4GeVrMmJH4hfFBRApe1StvhJSk4jcbT6bArQ'
let USDN = base58'DG2xFkPdDwKUoBkzGAhQtLpSGzfXLiCYPEzeKH2Ad24p'

let maxFeeAmountUSDN = 270000
let maxFeeAmountWaves = 900000

let keyPrefixPoolName = "pool_"

@Verifier(tx)
func verify() = match tx {
  case invoke: InvokeScriptTransaction => {
    let feeIsOk = if ((invoke.feeAssetId == unit && invoke.fee <= maxFeeAmountWaves)
        || (invoke.feeAssetId == USDN && invoke.fee <= maxFeeAmountUSDN)) then true
        else false
    let isExchangeCall = oracleAddress.getString(keyPrefixPoolName + invoke.dApp.addressFromRecipient().toString()).isDefined() && invoke.function == "exchange"
    let isAirdropCall = invoke.dApp == governanceAddress && invoke.function == "airDrop"
    let isUpdateWeightsCall = invoke.dApp == governanceAddress && invoke.function == "updateWeights"
    let isUpdatePoolInterest = invoke.dApp == farmingAddress && invoke.function == "updatePoolInterest"
    let signedByAdmin = sigVerify(tx.bodyBytes, tx.proofs[0], adminInvoke)

    (((isExchangeCall || isAirdropCall) && invoke.payments.size() == 1) || ((isUpdateWeightsCall || isUpdatePoolInterest) && invoke.payments.size() == 0)) && signedByAdmin && feeIsOk
  }
  case _ => {
    let adminPubKey1Signed = if sigVerify(tx.bodyBytes, tx.proofs[0], adminPubKey1) then 1 else 0
    let adminPubKey2Signed = if sigVerify(tx.bodyBytes, tx.proofs[1], adminPubKey2) then 1 else 0
    let adminPubKey3Signed = if sigVerify(tx.bodyBytes, tx.proofs[2], adminPubKey3) then 1 else 0
    adminPubKey1Signed + adminPubKey2Signed + adminPubKey3Signed >= 2
  }
}
